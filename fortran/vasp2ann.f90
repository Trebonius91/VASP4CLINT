!
!    vasp2ann: This program takes the output of either an nudged 
!      elastic band (NEB) calculation or an on-the-fly machine
!      learning force field (ML-FF) calculation and generates
!      input files containing structures, energies and gradients
!      that can be used to fit an artificial neural network (ANN)
!      potential for the system of interest with the aenet program
!      from atomisticnet on github (see there for details)      
!    Part of VASP4CLINT
!     Julien Steffen, 2023 (julien.steffen@fau.de)
!

program vasp2ann
implicit none
integer::i,j,k,inc,inc2,inc3
logical::eval_neb,eval_mlff,does_exist
character(len=120)::arg
character(len=160)::a160
character(len=80)::a80
character(len=50)::basename,out_name
character(len=3)::neb_paths(100)
real(kind=8)::energy
real(kind=8)::cell_vecs(3,3)
real(kind=8),allocatable::xyz(:,:)
real(kind=8),allocatable::grad(:,:)
character(len=2),allocatable::at_names(:)
integer::readstat
integer::nfolders,nelems,natoms,nstrucs
integer::el_nums(10)
character(len=2)::el_syms(10)

write(*,*) "PROGRAM vasp2ann: Transforms the output of a nudged elastic band (NEB)"
write(*,*) " or an on-the-fly VASP ML-FF generation trajectory to the training set"
write(*,*) " format needed by the atomisticnet/aenet framework on github to generate"
write(*,*) " an artificial neural network (ANN) potential for the system of interest."
write(*,*) "For NEB calculations, start the program in the main folder where "
write(*,*) " the frames are stored in subfolders 00,01,02, ..."
write(*,*) "For ML-FF calculation, start the program where the ML_AB file generated"
write(*,*) " generated by the on-the-fly learning of interest is located."
write(*,*) "The command line argument decides which kind of calculation will be done:"
write(*,*) " -neb : evaluates a NEB calculation."
write(*,*) " -ml_ff : evaluates a ML-FF learning calculation."
write(*,*) " -name=[speficier] : give a unique string to specify the current calculation"
write(*,*) "After execution, a number of XSF files (one for each structure) is written"
write(*,*) " into a new folder named 'xsf_files'. Further, a list of all file names"
write(*,*) " is written into the file 'xsf_list.dat'. The file names begin with the "
write(*,*) " basename defined by the user with the -name command (see above)."

!
!     If a NEB shall be evaluated
!
eval_neb = .false.
do i = 1, command_argument_count()
   call get_command_argument(i, arg)
   if (trim(arg)  .eq. "-neb") then
      eval_neb = .true.
   end if
end do

!
!     If a ML-FF (ML_AB) shall be evaluated
!
eval_mlff = .false.
do i = 1, command_argument_count()
   call get_command_argument(i, arg)
   if (trim(arg)  .eq. "-ml_ff") then
      eval_mlff = .true.
   end if
end do

!
!     Which basename the written XSF files shall have
!
basename="xxxxx"
do i = 1, command_argument_count()
   call get_command_argument(i, arg)
   if (trim(arg(1:6))  .eq. "-name=") then
      read(arg(7:),*) basename
   end if
end do

if (.not. eval_neb .and. .not. eval_mlff) then
   write(*,*) "Please choose either the -neb or the -ml_ff option!"
   stop
end if
if (eval_neb .and. eval_mlff) then
   write(*,*) "Please choose either the -neb or the -ml_ff option!"
   stop
end if
if (trim(basename) .eq. "xxxxx") then
   write(*,*) "Please give a basename for the XSF files with the -name keyword!"
   stop
end if        
!
!     Generate the folder containing the XFS files with the output
!     information
!
inquire(file="xsf_files/",exist=does_exist)
if (.not. does_exist) then
   call system("mkdir xsf_files")
end if
!
!     Open the file listening the names of all XFS files
!
open(unit=45,file="xsf_list.dat",status="replace")
write(45,*) "# This file contains the names of all XSF files, in the formate"
write(45,*) "# needed to setup the aenet calculation."
write(45,*) "# Copy the following lines into the ... input file."
write(45,*)
!
!     Evaluate a NEB calculation
!
if (eval_neb) then
!
!     Determine the number of NEB frames
!   
   i=1
   write(neb_paths(1),'(i1,i1,a)') 0,0,"/"
   do
      if (i .lt. 10) then
         write(neb_paths(i+1),'(i1,i1,a)') 0,i,"/"    
      else     
         write(neb_paths(i+1),'(i2,a)') i,"/"
      end if  
      inquire(file=neb_paths(i+1), exist=does_exist)
      if (.not. does_exist) exit
      i=i+1
   end do
   nfolders=i
   write(*,*) "There are ",nfolders-1," NEB frames present."
!
!     Now go into all folders and open the OUTCAR files
!     For frame 00 and N+1, the OUTCARs are viewed as optional
!
   do i=1,nfolders
      el_nums=0
      inc3=1
      call chdir(neb_paths(i))
      inquire(file="OUTCAR", exist=does_exist)
      if (.not. does_exist) then
         if (i .eq. 1 .or. i .eq. nfolders) then
            write(*,*) "Folder ",neb_paths(i)," with no OUTCAR skipped."
            call chdir("..")
            cycle
         else 
            write(*,*) "No OUTCAR file could be found in the folder",neb_paths(i),"!"
            stop
         end if        
      end if
!
!     Read in the relevant informations and write them into the XSF folder 
!
      open(unit=11,file="OUTCAR",status="old")
      do 
         read(11,'(a)',iostat=readstat) a160
         if (readstat .ne. 0) exit
!
!     Number of atoms and element symbols of them
!
         if (index(a160,"ions per type =") .ne. 0) then
            read(a160,*,iostat=readstat) a80,a80,a80,a80,el_nums
            do j=1,10
               if (el_nums(j) .ne. 0) then
                  nelems=j
               end if     
            end do
            natoms=sum(el_nums(1:nelems))
            if (allocated(xyz)) deallocate(xyz)
            allocate(xyz(3,natoms))
            if (allocated(grad)) deallocate(grad)
            allocate(grad(3,natoms))
            if (allocated(at_names)) deallocate(at_names)
            allocate(at_names(natoms))
!
!     Determine element symbols for all atoms
!        
            at_names="XX"
            inc2=1
            do j=1,nelems
               do k=1,el_nums(j)
                  at_names(inc2)=el_syms(j)
                  inc2=inc2+1
               end do
            end do

         end if   
         if (index(a160,"TITEL") .ne. 0) then
            read(a160,*) a80,a80,a80,el_syms(inc3)
            inc3=inc3+1
         end if        
!
!     Read energy, basis vectors, positions and gradients for current frame
!

         if (index(a160,'FREE ENERGIE OF THE ION-ELECTRON SYSTEM (eV)').ne.0) then
            read(11,*)
            read(11,*)
            read(11,*)
            read(11,*) a80,a80,a80,a80,a80,a80,energy
         end if
         if(index(a160,'VOLUME and BASIS-vectors').ne.0)then
            read(11,*)
            read(11,*)
            read(11,*)
            read(11,*)
            read(11,*) cell_vecs(1,:)
            read(11,*) cell_vecs(2,:)
            read(11,*) cell_vecs(3,:)
         end if
         if (index(a160,'POSITION                                       TOTAL-FORCE (eV/Angst)') &
                         & .ne. 0) then
            read(11,*)
            do j=1,natoms
               read(11,*) xyz(:,j),grad(:,j)
            end do
         end if        

      end do
      close(11)
      call chdir("..")
!
!     Write output file for current frame
!
      if (i-1 .lt. 10) then
         write(out_name,'(a,a,i1)') trim(basename),"_frame",i-1
      else
         write(out_name,'(a,a,i2)') trim(basename),"_frame",i-1
      end if
      open(unit=30,file="xsf_files/" // trim(out_name) // ".xsf")
      write(30,'(a,f14.8,a)') "# total energy = ",energy," eV"
      write(30,*)
      write(30,'(a)') "CRYSTAL"
      write(30,'(a)') "PRIMVEC"
      do j=1,3
         write(30,'(3f14.8)') cell_vecs(j,:)
      end do
      write(30,'(a)') "PRIMCOORD"
      write(30,'(i6,i1)') natoms,1
      do j=1,natoms
         write(30,'(a,a,6f14.8)') at_names(j)," ",xyz(:,j),grad(:,j)
      end do

      close(30)
!
!     Write name of output file to list of filenames
!
      write(45,'(a,a,a)') "./xsf_files/", trim(out_name),".xsf"

   end do

end if        

!
!     Evaluate a ML-FF calculation (ML_AB file)
!
if (eval_mlff) then
!
!     Try to open the ML_AB file, abort if its not there
!
   inquire(file="ML_AB", exist=does_exist)
   if (.not. does_exist) then
      write(*,*) "The file ML_AB is not there!"
      stop
   end if         
   open(unit=27,file="ML_AB",status="old")
   read(27,*)
   read(27,*)
   read(27,*)
   read(27,*)
   read(27,*) nstrucs
   write(*,*) "Number of structures in the ML_AB fille: ",nstrucs
   inc=0
   do
      read(27,'(a)',iostat=readstat) a160
      if (readstat .ne. 0) exit
!
!     Read in all information for the current configuration
!
      if (index(a160,"Configuration num.") .ne. 0) then
         do i=1,7
            read(27,*)
         end do
         read(27,*) nelems
         do i=1,3
            read(27,*) 
         end do
         read(27,*) natoms
         do i=1,3
            read(27,*)
         end do
         do i=1,nelems
            read(27,*) el_syms(i),el_nums(i)
         end do      
         do i=1,7
            read(27,*)
         end do
         read(27,*) cell_vecs(1,:)
         read(27,*) cell_vecs(2,:)
         read(27,*) cell_vecs(3,:)
         do i=1,3
            read(27,*)
         end do
         if (allocated(xyz)) deallocate(xyz)
         allocate(xyz(3,natoms))
         if (allocated(grad)) deallocate(grad)
         allocate(grad(3,natoms))
         if (allocated(at_names)) deallocate(at_names)
         allocate(at_names(natoms))
!
!     Determine element symbols for all atoms
!
         at_names="XX"
         inc2=1
         do j=1,nelems
            do k=1,el_nums(j)
               at_names(inc2)=el_syms(j)
               inc2=inc2+1
            end do
         end do

         do i=1,natoms
            read(27,*) xyz(:,i)
         end do
         do i=1,3
            read(27,*)
         end do
         read(27,*) energy
         do i=1,3
            read(27,*)
         end do
         do i=1,natoms
            read(27,*) grad(:,i)
         end do
         inc=inc+1
!
!     Write the main output file for current structure
!
         if (inc .lt. 10) then
            write(out_name,'(a,a,i1)') trim(basename),"_struc",inc
         else if (inc .lt. 100) then 
            write(out_name,'(a,a,i2)') trim(basename),"_struc",inc
         else if (inc .lt. 1000) then
            write(out_name,'(a,a,i3)') trim(basename),"_struc",inc
         else if (inc .lt. 10000) then
            write(out_name,'(a,a,i4)') trim(basename),"_struc",inc
         else
            write(out_name,'(a,a,i5)') trim(basename),"_struc",inc
         end if        
         open(unit=30,file="xsf_files/" // trim(out_name) // ".xsf")
         write(30,'(a,f14.8,a)') "# total energy = ",energy," eV"
         write(30,*)
         write(30,'(a)') "CRYSTAL"
         write(30,'(a)') "PRIMVEC"
         do j=1,3
            write(30,'(3f14.8)') cell_vecs(j,:)
         end do
         write(30,'(a)') "PRIMCOORD"
         write(30,'(i6,i1)') natoms,1
         do j=1,natoms
            write(30,'(a,a,6f14.8)') at_names(j)," ",xyz(:,j),grad(:,j)
         end do

         close(30)
!
!     Write name of output file to list of filenames
!
         write(45,'(a,a,a)') "./xsf_files/", trim(out_name),".xsf"

      end if   
      if (inc .gt. nstrucs) exit
   end do

   close(27)

end if        

close(45)
write(*,*)
write(*,*) "XSF files written to folder 'xsf_files'"
write(*,*) "List of file names written to 'xsf_list.dat'"
write(*,*)

end program vasp2ann      

